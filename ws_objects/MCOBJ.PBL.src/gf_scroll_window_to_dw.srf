$PBExportHeader$gf_scroll_window_to_dw.srf
global type gf_scroll_window_to_dw from function_object
end type

forward prototypes
global subroutine gf_scroll_window_to_dw (ref window aw_window, datawindow adw_datawindow)
end prototypes

global subroutine gf_scroll_window_to_dw (ref window aw_window, datawindow adw_datawindow);// setting tag so we know adw_datawindow is scrolling
adw_datawindow.tag = "scroll"

// script variables
integer	li_difference, li_last, li_Mark
integer	li_LRPageStep = -150, li_UDPageStep = 80
integer	li_BottomBorder = 175, li_RightBorder = 115	// constants

// make sure the window is still open
If Not(IsValid(aw_Window)) Then Return

If aw_Window.WindowState = Minimized! Then Return

// make sure the datawindow is redrawing, otherwise X and Y might not move
adw_datawindow.SetRedraw(True)

// DO LEFT AND RIGHT SCROLLING
// If the left edge of the datawindow is off the left edge of the window
// then we have to scroll the window to the left to bring it back on the screen
If adw_datawindow.x < 1 Then
	Do
		If Abs(adw_datawindow.x) > Abs(li_LRPageStep) Then
			gf_scroll_win_left_page(aw_Window)
		Else
			gf_scroll_win_left(aw_Window)
		End If
		// adw_datawindow yield allows the window to redraw, changing the X coordinate of the dw
		// allowing a correct test for when we're done scrolling
		Yield()
		// make sure the window is still open
		If Not(IsValid(aw_Window)) Then Return
		
	Loop Until adw_datawindow.x > 0
Else
	// check if the right edge of the datawindow is off the right side of the window
	// we only want to do adw_datawindow if we didn't scroll left to pull the left edge on the screen
	li_Mark = adw_datawindow.x + adw_datawindow.Width + li_RightBorder
	li_Difference = aw_Window.Width - li_Mark
	Do While li_Difference < 0
		If (Abs(li_Difference) > Abs(li_LRPageStep)) AND (adw_datawindow.x > Abs(li_LRPageStep)) Then
			gf_scroll_win_right_page(aw_Window)
		Else
			If adw_datawindow.x < 10 Then exit
			gf_scroll_win_right(aw_Window)
		End If
		Yield()
		// make sure the window is still open
		If Not(IsValid(aw_Window)) Then Return

		// storing the last difference to make sure the scrolls are actually scrolling
		// had some problems with different resolutions and screen sizes that the window
		// size wasn't big enough to hold all of the datawindows.  To go to the footer it 
		// would say, "I need to scroll down", send the scroll down message, didn't move 
		// because it was at the bottom of the scrollable area.
		li_Last = li_Difference
		li_Mark = adw_datawindow.x + adw_datawindow.Width + li_RightBorder
		li_Difference = aw_Window.Width - li_Mark

		If li_Last = li_Difference Then
			// it thinks it needs to scroll more, but the scroll didn't move it any
			// make sure the window is wide enough, and let it try to scroll again
			If aw_Window.Width >= li_Mark + 5 Then
				// there shouldn't be a problem, why isn't it working???
				Exit
			Else
				aw_Window.Width = li_Mark + 5
			End If
		End If
	Loop
End If 

// DO UP AND DOWN SCROLLING
// If the top edge of the datawindow is off the top of the window
// then we have to scroll the window up to bring it back on the screen
If adw_datawindow.y < 1 Then
	Do
		If Abs(adw_datawindow.y) > Abs(li_UDPageStep) Then
			gf_scroll_win_up_page(aw_Window)
		Else
			gf_scroll_win_up(aw_Window)
		End If
		// adw_datawindow yield allows the window to redraw, changing the Y coordinate of the dw
		// allowing a correct test for when we're done scrolling
		Yield()
		// make sure the window is still open
		If Not(IsValid(aw_Window)) Then Return
		
	Loop Until adw_datawindow.y > 0
Else
	// check if the bottom edge of the datawindow is off the bottom of the window
	// we only want to do adw_datawindow if we didn't scroll up to pull the top edge on the screen
	li_Mark = adw_datawindow.y + adw_datawindow.height + li_BottomBorder
	li_Difference = aw_Window.Height - li_Mark
	Do While li_Difference < 0
		If (Abs(li_Difference) > Abs(li_UDPageStep)) AND (adw_datawindow.y > Abs(li_UDPageStep)) Then
			gf_scroll_win_down_page(aw_Window)
		Else
			If adw_datawindow.y < 10 Then exit
			gf_scroll_win_down(aw_Window)
		End If
		Yield()
		// make sure the window is still open
		If Not(IsValid(aw_Window)) Then Return

		// storing the last difference to make sure the scrolls are actually scrolling
		// had some problems with different resolutions and screen sizes that the window
		// size wasn't big enough to hold all of the datawindows.  To go to the footer it 
		// would say, "I need to scroll down", send the scroll down message, didn't move 
		// because it was at the bottom of the scrollable area.
		li_Last = li_Difference
		li_Mark = adw_datawindow.y + adw_datawindow.height + li_BottomBorder
		li_Difference = aw_Window.Height - li_Mark

		If li_Last = li_Difference Then
			// it thinks it needs to scroll more, but the scroll didn't move it any
			// make sure the window is wide enough, and let it try to scroll again
			If aw_Window.Height >= li_Mark + 5 Then
				// there shouldn't be a problem, why isn't it working???
				Exit
			Else
				aw_Window.Height = li_Mark + 5
			End If
		End If
	Loop
End If 

// turn scroll tag off
adw_datawindow.tag = ""

end subroutine

